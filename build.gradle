buildscript {

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
        classpath("se.transmode.gradle:gradle-docker:1.2")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2")
    }

}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

jar {

    baseName = 'treeset'
    version = '0.1.0'

}

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {

    compile 'org.slf4j:slf4j-api:1.7.25'
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.hsqldb:hsqldb")
    compile("io.springfox:springfox-swagger2:2.6.1")
    compile("io.springfox:springfox-swagger-ui:2.6.1")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile 'junit:junit:4.12'
    testCompile group: 'com.google.code.gson', name: 'gson', version: '2.7'

}

docker {
    maintainer "Bartosz Wojcik <b.wojcik@ximago.pl>"
    baseImage "anapsix/alpine-java"
}

jacoco {
    toolVersion = "0.8.2"
    reportsDir = file("$buildDir/reports")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/config/**',
                              '**/Application**'])
        })
    }
}

coveralls {
    jacocoReportPath 'build/reports/test/jacocoTestReport.xml'
}

task wrapper (type: Wrapper) {
    gradleVersion = '4.3.1'
}

bootRun {
    args = ['--spring.profiles.active=swagger']
}
